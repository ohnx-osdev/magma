# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//magma/gnbuild/magma.gni")

declare_args() {
  unit_test = true
  readback_test = true
  spinning_cube_test = false
}

source_set("main") {
  configs += [ "$magma_build_root:magma_tests_include_config" ]

  sources = [
    "driver_main.cpp",
  ]
  deps = [
    "$magma_build_root:libmsd",
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/magma_util/platform/magenta",
  ]

  defines = []

  if (unit_test) {
    deps += [
      "$magma_build_root/tests/unit_tests:magma_platform_tests",
      "$magma_build_root/tests/unit_tests:msd_abi_conformance_tests",
      "//third_party/gtest",
    ]
    defines += [ "MAGMA_UNIT_TESTS=1" ]
  }

  if (readback_test) {
    deps += [ "$magma_build_root/tests/gpureadback" ]
    defines += [ "MAGMA_READBACK_TEST=1" ]
  } else if (spinning_cube_test) {
    deps += [ "$magma_build_root/tests/spinning_cube" ]
    defines += [ "MAGMA_SPINNING_CUBE_TEST=1" ]
  }

  libs = [
    "driver",
    "ddk",
    "magenta",
  ]
}
