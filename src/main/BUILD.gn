# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//magma/gnbuild/magma.gni")

declare_args() {
  unit_test = true
  vkreadback_test = false
  glreadback_test = false
  spinning_cube_test = false
}

source_set("main") {
  sources = [
    "driver_main.cpp",
  ]

  deps = [
    "$magma_build_root/src/sys_driver",
    "$magma_build_root/src/magma_util",
  ]

  libs = [
    "driver",
    "ddk",
    "magenta",
  ]
}

source_set("test_driver") {
  testonly = true

  configs += [ "$magma_build_root:magma_tests_include_config" ]

  sources = [
    "driver_main.cpp",
  ]

  deps = [
    "$magma_build_root/src/sys_driver",
    "$magma_build_root/src/magma_util",
    "$magma_build_root/tests/helper:platform_device_helper",
  ]

  defines = []

  # We want to always build gl, for now.
  deps += [ "$magma_build_root/tests/gpureadback" ]

  if (unit_test) {
    defines += [ "MAGMA_UNIT_TESTS=1" ]
    deps += [
      "$magma_build_root/tests/unit_tests:magma_platform_tests",
      "$magma_build_root/tests/unit_tests:msd_abi_conformance_tests",
      "//third_party/gtest",
    ]
  }
  if (glreadback_test) {
    defines += [ "MAGMA_GLREADBACK_TEST=1" ]
  }
  if (vkreadback_test) {
    # Vulkan and gl collide linkerwise.
    deps -= [ "$magma_build_root/tests/gpureadback" ]
    deps += [ "$magma_build_root/tests/vkreadback" ]
    defines += [ "MAGMA_VKREADBACK_TEST=1" ]
  }
  if (spinning_cube_test) {
    deps += [ "$magma_build_root/tests/spinning_cube" ]
    defines += [ "MAGMA_SPINNING_CUBE_TEST=1" ]
  }

  libs = [
    "driver",
    "ddk",
    "magenta",
  ]
}
